#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНачисленияПоСотрудникам();
	Объект.ВКМ_СписокСотрудниковОсновное.Сортировать("ВКМ_Сотрудник Возр");
	//ЗаполнитьНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНачисленияПоСотрудникам()	
	
	Объект.ВКМ_СписокСотрудниковОсновное.Очистить();
	
	//1. Все сотрудники + Всего рабочих дней + Рабочие дни которые попали в период отпуска
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|    Сотр.Ссылка КАК Сотрудник,
	|    РабочиеДниПериода.РабочиеДниВсего КАК РабочиеДниВсего,
	|    КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Данные.ВКМ_Дата) КАК РабочиеДниВОтпуске
	|ИЗ
	|    Справочник.Пользователи КАК Сотр
	|    ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВКМ_ГрафикОтпусков.ВКМ_ОтпускаСотрудников КАК Отпуска
	|        ПО Сотр.Ссылка = Отпуска.ВКМ_Сотрудник
	|        И ГОД(Отпуска.Ссылка.ВКМ_Год) = ГОД(&ДатаРасчета)
	|    ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|        ВЫБРАТЬ
	|            КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДатаВсего.ВКМ_Дата) КАК РабочиеДниВсего
	|        ИЗ
	|            РегистрСведений.ВКМ_ДанныеГрафика КАК ДатаВсего
	|        ГДЕ
	|            ДатаВсего.ВКМ_РабочийДень = 1
	|            И ДатаВсего.ВКМ_Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|    ) КАК РабочиеДниПериода
	|    ПО ИСТИНА
	|    ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_ДанныеГрафика КАК Данные
	|        ПО
	|            Данные.ВКМ_РабочийДень = 1
	|            И Данные.ВКМ_Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|            И Данные.ВКМ_Дата МЕЖДУ Отпуска.ВКМ_ДатаНачала И Отпуска.ВКМ_ДатаОкончания
	|ГДЕ
	|    Сотр.ПометкаУдаления = ЛОЖЬ
	|    И Сотр.Служебный = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|    Сотр.Ссылка,
	|    Сотр.Наименование,
	|    РабочиеДниПериода.РабочиеДниВсего";
	
	Запрос.УстановитьПараметр("ДатаРасчета", Объект.Дата);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Объект.Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Объект.Дата));
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	РабочиеДниВМесяце = Неопределено;
	КоличествоРабочихДнейСотрудников = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		РабочиеДниФактические = Выборка.РабочиеДниВсего - Выборка.РабочиеДниВОтпуске;
		КоличествоРабочихДнейСотрудников.Вставить(Выборка.Сотрудник, РабочиеДниФактические);
		РабочиеДниВМесяце = Выборка.РабочиеДниВсего;
	КонецЦикла;
	
	// 2.0 Подготовка данных
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|    Сотр.Ссылка КАК Сотрудник,
	|    ЕСТЬNULL(СУММА(Начисления.СуммаНачислений), 0) /
	|        ЕСТЬNULL(СУММА(РабочиеДни.РабочиеДниМесяца), 1) КАК СреднедневнойЗаработок
	|ИЗ
	|    Справочник.Пользователи КАК Сотр
	|    ЛЕВОЕ СОЕДИНЕНИЕ (
	|        ВЫБРАТЬ
	|            Взаиморасчеты.ВКМ_Сотрудник КАК Сотрудник,
	|            ГОД(Взаиморасчеты.Период) КАК Год,
	|            МЕСЯЦ(Взаиморасчеты.Период) КАК Месяц,
	|            СУММА(Взаиморасчеты.ВКМ_Сумма) КАК СуммаНачислений
	|        ИЗ
	|            РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками КАК Взаиморасчеты
	|        ГДЕ
	|            Взаиморасчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|            И Взаиморасчеты.Период МЕЖДУ ДОБАВИТЬКДАТЕ(&ДатаРасчета, МЕСЯЦ, -12) И &ДатаРасчета
	|        СГРУППИРОВАТЬ ПО
	|            Взаиморасчеты.ВКМ_Сотрудник,
	|            ГОД(Взаиморасчеты.Период),
	|            МЕСЯЦ(Взаиморасчеты.Период)
	|    ) КАК Начисления
	|    ПО Сотр.Ссылка = Начисления.Сотрудник
	|    ЛЕВОЕ СОЕДИНЕНИЕ (
	|        ВЫБРАТЬ
	|            ГОД(Данные.ВКМ_Дата) КАК Год,
	|            МЕСЯЦ(Данные.ВКМ_Дата) КАК Месяц,
	|            КОЛИЧЕСТВО(Данные.ВКМ_Дата) КАК РабочиеДниМесяца
	|        ИЗ
	|            РегистрСведений.ВКМ_ДанныеГрафика КАК Данные
	|        ГДЕ
	|            Данные.ВКМ_РабочийДень = 1
	|        СГРУППИРОВАТЬ ПО
	|            ГОД(Данные.ВКМ_Дата),
	|            МЕСЯЦ(Данные.ВКМ_Дата)
	|    ) КАК РабочиеДни
	|    ПО РабочиеДни.Год = Начисления.Год
	|        И РабочиеДни.Месяц = Начисления.Месяц
	|ГДЕ
	|    Сотр.ПометкаУдаления = ЛОЖЬ
	|    И Сотр.Служебный = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|    Сотр.Ссылка";
	
	Запрос.УстановитьПараметр("ДатаРасчета", Объект.Дата);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СредниеЗаработкиСотрудников = Новый Соответствие;	
	Пока Выборка.Следующий() Цикл
		СредниеЗаработкиСотрудников.Вставить(Выборка.Сотрудник, Выборка.СреднедневнойЗаработок);
	КонецЦикла;
	
	// 2. Все дни отпуска в периоде
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Отпуска.ВКМ_Сотрудник КАК Сотрудник,
	|	СУММА(ВЫБОР
	|			КОГДА Отпуска.ВКМ_ДатаНачала <= &ДатаОкончания
	|					И Отпуска.ВКМ_ДатаОкончания >= &ДатаНачала
	|				ТОГДА РАЗНОСТЬДАТ(ВЫБОР
	|							КОГДА Отпуска.ВКМ_ДатаНачала > &ДатаНачала
	|								ТОГДА Отпуска.ВКМ_ДатаНачала
	|							ИНАЧЕ &ДатаНачала
	|						КОНЕЦ, ДОБАВИТЬКДАТЕ(ВЫБОР
	|								КОГДА Отпуска.ВКМ_ДатаОкончания < &ДатаОкончания
	|									ТОГДА Отпуска.ВКМ_ДатаОкончания
	|								ИНАЧЕ &ДатаОкончания
	|							КОНЕЦ, ДЕНЬ, 1), ДЕНЬ)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ДниОтпускаВПериоде
	|ИЗ
	|	Документ.ВКМ_ГрафикОтпусков.ВКМ_ОтпускаСотрудников КАК Отпуска
	|ГДЕ
	|	ГОД(Отпуска.Ссылка.ВКМ_Год) = ГОД(&ДатаРасчета)
	|
	|СГРУППИРОВАТЬ ПО
	|	Отпуска.ВКМ_Сотрудник";
	
	Запрос.УстановитьПараметр("ДатаРасчета", Объект.Дата);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Объект.Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Объект.Дата));
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ДниОтпускаВПериоде) Тогда
			ЗаписьПоОтпуску = Объект.ВКМ_СписокСотрудниковОсновное.Добавить();
			ЗаписьПоОтпуску.ВКМ_Сотрудник = Выборка.Сотрудник;	
			ЗаписьПоОтпуску.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск;	
			ЗаписьПоОтпуску.ВКМ_ДатаНачала = НачалоМесяца(Объект.Дата);	
			ЗаписьПоОтпуску.ВКМ_ДатаОкончания = КонецМесяца(Объект.Дата);
			
			Сотрудник = Выборка.Сотрудник;
			СреднийЗаработокСотрудника = СредниеЗаработкиСотрудников.Получить(Сотрудник);
			Если Не ЗначениеЗаполнено(СреднийЗаработокСотрудника) Тогда
				Продолжить;
			КонецЕсли;
			Сумма = СреднийЗаработокСотрудника * Выборка.ДниОтпускаВПериоде;		
			ЗаписьПоОтпуску.ВКМ_Начислено = Сумма;
			
			ЗаписьПоОтпуску.ВКМ_НДФЛ = Окр(Сумма * 0.13, 2);
			ЗаписьПоОтпуску.ДниФактСкрытое = Выборка.ДниОтпускаВПериоде;
		КонецЕсли;
	КонецЦикла;
	
	// 3. Все оклады сотрудников + Начисленные суммы по проценту от работ
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Сотр.Ссылка КАК Сотрудник,
	|	ЕСТЬNULL(УслОП.ВКМ_Оклад, 0) КАК Оклад,
	|	ЕСТЬNULL(ВыпРаб.СуммаКОплате, 0) КАК СуммаЗаВыполненыеРаботыПроцентом
	|	ИЗ
	|	Справочник.Пользователи КАК Сотр
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Условия.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	|			Условия.ВКМ_Оклад КАК ВКМ_Оклад
	|		ИЗ
	|			РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&ДатаРасчета,) КАК Условия) КАК УслОП
	|		ПО Сотр.Ссылка = УслОП.ВКМ_Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Выполненные.ВКМ_Сотрудник КАК ВКМ_Сотрудник,
	|			СУММА(Выполненные.ВКМ_СуммаКОплатеОборот) КАК СуммаКОплате
	|		ИЗ
	|			РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&ПериодНачала, &ПериодОкончания,,) КАК Выполненные
	|		СГРУППИРОВАТЬ ПО
	|			Выполненные.ВКМ_Сотрудник) КАК ВыпРаб
	|		ПО Сотр.Ссылка = ВыпРаб.ВКМ_Сотрудник
	|ГДЕ
	|	Сотр.ПометкаУдаления = ЛОЖЬ
	|	И Сотр.Служебный = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ДатаРасчета", Объект.Дата);
	Запрос.УстановитьПараметр("ПериодНачала", НачалоМесяца(Объект.Дата));
	Запрос.УстановитьПараметр("ПериодОкончания", КонецМесяца(Объект.Дата));
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Сотрудник = Выборка.Сотрудник;
		РабочиеДниФактические = КоличествоРабочихДнейСотрудников.Получить(Сотрудник);
		Если Не ЗначениеЗаполнено(РабочиеДниФактические) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.Оклад) Тогда
			ОкладЗаДень = Окр(Выборка.Оклад / РабочиеДниВМесяце,2);
			ДляНачисления = Окр(РабочиеДниФактические * ОкладЗаДень,2);
			
			ЗаписьПоОкладу = Объект.ВКМ_СписокСотрудниковОсновное.Добавить();
			ЗаписьПоОкладу.ВКМ_Сотрудник = Выборка.Сотрудник;	
			ЗаписьПоОкладу.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ОплатаПоОкладу;	
			ЗаписьПоОкладу.ВКМ_ДатаНачала = НачалоМесяца(Объект.Дата);	
			ЗаписьПоОкладу.ВКМ_ДатаОкончания = КонецМесяца(Объект.Дата);	
			ЗаписьПоОкладу.ВКМ_Начислено = ДляНачисления;
			ЗаписьПоОкладу.ВКМ_НДФЛ = Окр(ДляНачисления * 0.13, 2);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.СуммаЗаВыполненыеРаботыПроцентом) Тогда
		ЗаписьПоПроценту = Объект.ВКМ_СписокСотрудниковОсновное.Добавить();
		ЗаписьПоПроценту.ВКМ_Сотрудник = Выборка.Сотрудник;	
		ЗаписьПоПроценту.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПроцентЗаВыполненныеРаботы;	
		ЗаписьПоПроценту.ВКМ_ДатаНачала = НачалоМесяца(Объект.Дата);	
		ЗаписьПоПроценту.ВКМ_ДатаОкончания = КонецМесяца(Объект.Дата);	
		ЗаписьПоПроценту.ВКМ_Начислено = Выборка.СуммаЗаВыполненыеРаботыПроцентом;
		ЗаписьПоПроценту.ВКМ_НДФЛ = Окр(Выборка.СуммаЗаВыполненыеРаботыПроцентом * 0.13, 2);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


