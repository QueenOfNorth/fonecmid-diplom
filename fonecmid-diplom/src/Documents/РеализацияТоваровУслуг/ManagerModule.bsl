
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// + ВКМ Павлова
Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт выполненных работ
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Акт";
	КомандаПечати.Представление = НСтр("ru = 'Акт выполненных работ'");
	КомандаПечати.Порядок = 5;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Акт");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАктаВыполненыхРабот(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт выполненных работ'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_MXL_Акт";
	КонецЕсли;
	
КонецПроцедуры
// - ВКМ Павлова


#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// + ВКМ Павлова
Функция ПечатьАктаВыполненыхРабот(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Акт";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_Акт");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиАкт(ДанныеДокументов, ТабличныйДокумент, Макет);
		
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	

	Возврат ТабличныйДокумент;
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РеализацияТоваровУслуг.Ссылка,
		|	РеализацияТоваровУслуг.Номер,
		|	РеализацияТоваровУслуг.Дата,
		|	РеализацияТоваровУслуг.Организация,
		|	РеализацияТоваровУслуг.Контрагент,
		|	РеализацияТоваровУслуг.Договор,
		|	РеализацияТоваровУслуг.Услуги.(
		|		НомерСтроки,
		|		Номенклатура,
		|		Количество,
		|		Цена,
		|		Сумма)
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Ссылка В (&МассивОбъектов)";
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Процедура ВывестиАкт(ДанныеДокументов, ТабличныйДокумент, Макет)

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьУслугиШапка = Макет.ПолучитьОбласть("УслугиШапка");
	ОбластьУслуги = Макет.ПолучитьОбласть("Услуги");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ОбластьЗаголовок.Параметры.ДатаПечати = Формат(ТекущаяДатаСеанса(),"ДЛФ=ДД");
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
	Шапка.Параметры.Заполнить(ДанныеДокументов);
	ТабличныйДокумент.Вывести(Шапка, ДанныеДокументов.Уровень());	
	ТабличныйДокумент.Вывести(ОбластьУслугиШапка);
	ВыборкаУслуги = ДанныеДокументов.Услуги.Выбрать();
	Сумма = 0;
	Пока ВыборкаУслуги.Следующий() Цикл
		ОбластьУслуги.Параметры.Заполнить(ВыборкаУслуги);
		Сумма = Сумма + ВыборкаУслуги.Сумма;
		ТабличныйДокумент.Вывести(ОбластьУслуги, ВыборкаУслуги.Уровень());
	КонецЦикла;
	ОбластьПодвал.Параметры.СуммаИтого = Сумма;
	ОбластьПодвал.Параметры.СуммаИтогоПрописью = ЧислоПрописью(Сумма,"Л=ru_RU; ДП=Истина","рубль, рубля, рублей, м, копейка, копейки, копеек, ж" );
	ТабличныйДокумент.Вывести(ОбластьПодвал);
КонецПроцедуры

Процедура СоздатьРеализации(Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК СсылкаДоговор,	
	|	ДоговорыКонтрагентов.Организация КАК Организация,
	|	ДоговорыКонтрагентов.Владелец КАК Владелец,
	|РеализацияТоваровУслуг.Ссылка КАК СсылкаРеализация
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО (ДоговорыКонтрагентов.Ссылка = РеализацияТоваровУслуг.Договор)
	|			И (РеализацияТоваровУслуг.ПометкаУдаления = ЛОЖЬ)
	|			И (РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНАЧАЛА И &ДатаОКОНЧАНИЯ)
	|ГДЕ
	|	ДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ
	|	И РеализацияТоваровУслуг.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ДатаНАЧАЛА", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОКОНЧАНИЯ", Период.ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для каждого Строка из РезультатЗапроса Цикл			
		НовыйДокумент = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		НовыйДокумент.Дата = ТекущаяДатаСеанса();
		НовыйДокумент.Договор = Строка.СсылкаДоговор;
		НовыйДокумент.Организация = Строка.Организация;
		НовыйДокумент.Контрагент = Строка.Владелец;
		НовыйДокумент.Ответственный = Пользователи.ТекущийПользователь();
		НовыйДокумент.Комментарий = "Создано обработкой ""Массовое создание актов""";
		НовыйДокумент.ВыполнитьАвтозаполнение();
		НовыйДокумент.Записать();
		Если НовыйДокумент.ПроверитьЗаполнение() Тогда			
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение); 
		КонецЕсли;	
	КонецЦикла;
     
КонецПроцедуры
// - ВКМ Павлова
#КонецОбласти

#КонецЕсли