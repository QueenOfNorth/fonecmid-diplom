#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ,Режим)
	
	//Проверка: нельзя проводить второй график отпусков на тот же год
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	График.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВКМ_ГрафикОтпусков КАК График
	|ГДЕ
	|	Год(График.ВКМ_Год) = Год(&Год)
	|	И График.Проведен
	|	И График.Ссылка <> &ТекущийДок";

	Запрос.УстановитьПараметр("Год", ВКМ_Год);
	Запрос.УстановитьПараметр("ТекущийДок", Ссылка);

	Результат = Запрос.Выполнить();

	Если НЕ Результат.Пустой() Тогда
		ОбщегоНазначения.СообщитьПользователю("График отпусков за " + Год(ВКМ_Год) + " год уже существует. Проведение отменено.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// регистр ВКМ_Отпуска
	Движения.ВКМ_Отпуска.Записывать = Истина;
	Для Каждого ТекСтрокаВКМ_ОтпускаСотрудников из ВКМ_ОтпускаСотрудников Цикл
		Движение = Движения.ВКМ_Отпуска.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.ВКМ_Сотрудник = ТекСтрокаВКМ_ОтпускаСотрудников.ВКМ_Сотрудник;
		Движение.ВКМ_ДниФакт = 0;
		Движение.ВКМ_ДниПлан = ТекСтрокаВКМ_ОтпускаСотрудников.РазностьДат;
	КонецЦикла;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	//Данный фрагмент построен конструктором.
	//При повторном использовании конструктора, внесенные вручную данные будут утеряны!
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	// Проверка пересечений отпусков
	Для Каждого Строка1 Из ВКМ_ОтпускаСотрудников Цикл
		Для Каждого Строка2 Из ВКМ_ОтпускаСотрудников Цикл
			Если Строка1 <> Строка2 Тогда
				
				Если Строка1.ВКМ_Сотрудник = Строка2.ВКМ_Сотрудник Тогда
					
					// Проверяем, пересекаются ли периоды
					Если (Строка1.ВКМ_ДатаНачала <= Строка2.ВКМ_ДатаОкончания) 
						И (Строка2.ВКМ_ДатаНачала <= Строка1.ВКМ_ДатаОкончания) Тогда
						
						ОбщегоНазначения.СообщитьПользователю("У сотрудника """ 
							+ Строка1.ВКМ_Сотрудник 
							+ """ пересекаются отпуска!");
						Отказ = Истина;
						Возврат;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли